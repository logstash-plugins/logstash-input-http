import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

plugins {
  id 'java'
  id 'idea'
  id 'maven-publish'  
}

group 'org.logstash.plugins.input.http'
version rootProject.file('VERSION').text.trim()

description = "HTTP Input Netty implementation"

String log4jVersion = '2.17.0'
String nettyVersion = '4.1.129.Final'
String junitVersion = '5.9.2'

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    // netty-codec-http pulls netty-handler, which pulls netty-buffer, netty-codec, netty-common,
    // netty-transport, netty-transport-native-unix-common, and netty-resolver as transitive dependencies
    implementation "io.netty:netty-codec-http:${nettyVersion}"

    compileOnly "org.apache.logging.log4j:log4j-api:${log4jVersion}" // provided by Logstash

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    testImplementation 'org.mockito:mockito-core:5.12.0'
}

test {
    useJUnitPlatform()
    systemProperties.put "io.netty.leakDetectionLevel", "paranoid"
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
}

task run (type: JavaExec, dependsOn: classes) {
    // turn on or off ssl
    if (project.hasProperty('providedargs')) {
        args(providedargs.split(','))
    }
    description = "Plugin testing"
    mainClass = "org.logstash.plugins.input.http.Runner"
    classpath = sourceSets.main.runtimeClasspath
}

task generateGemJarRequiresFile {
    doLast {
        File jars_file = file("lib/${project.name}_jars.rb")
        jars_file.newWriter().withWriter { w ->
            w << "# AUTOGENERATED BY THE GRADLE SCRIPT. DO NOT EDIT.\n\n"
            w << "require \'jar_dependencies\'\n"
            configurations.runtimeClasspath.incoming.artifacts.artifacts.each { artifact ->
                def id = artifact.id.componentIdentifier
                if (id instanceof org.gradle.api.artifacts.component.ModuleComponentIdentifier) {
                    w << "require_jar(\'${id.group}\', \'${id.module}\', \'${id.version}\')\n"
                }
            }
        }
    }
}

task vendor {
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        String projectGroupPath = project.group.replaceAll('\\.', '/')
        configurations.runtimeClasspath.incoming.artifacts.artifacts.each { artifact ->
            def id = artifact.id.componentIdentifier
            if (id instanceof org.gradle.api.artifacts.component.ModuleComponentIdentifier) {
                String name = id.module
                String version = id.version
                File newJarFile = file("${vendorPathPrefix}/${projectGroupPath}/${name}/${version}/${name}-${version}.jar")
                if (!newJarFile.parentFile.exists()) {
                    newJarFile.parentFile.mkdirs()
                }
                Files.copy(artifact.file.toPath(), newJarFile.toPath(), REPLACE_EXISTING)
            }
        }

        File projectJarFile = file("${vendorPathPrefix}/${projectGroupPath}/${project.name}/${project.version}/${project.name}-${project.version}.jar")
        projectJarFile.mkdirs()
        Files.copy(file("$buildDir/libs/${project.name}-${project.version}.jar").toPath(), projectJarFile.toPath(), REPLACE_EXISTING)
    }
}

vendor.dependsOn(jar, generateGemJarRequiresFile)

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
}
